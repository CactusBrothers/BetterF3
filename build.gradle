plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'java-library'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

loom {
    accessWidenerPath.set(file("src/main/resources/betterf3.accesswidener"))
}

repositories {
    maven {
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
}

configurations {
    shadow
    implementation.extendsFrom shadow
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
        exclude module: 'fabric-api'
    }
    include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}", {
        exclude module: 'fabric-api'
    }
    include(modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version)))

    shadow "com.electronwill.night-config:json:3.6.3"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

}


processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

}


// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


jar {
    from "LICENSE"

    from {
        configurations.shadow.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

}

